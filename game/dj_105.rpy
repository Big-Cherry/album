# ###########################################################
#            Copyright (c) 2020 BigCherry Team
#                   All Rights Reserved 
#
#  filename: dj_105.rpy
#  description: (story scripts) Dongji Main Story 10.5
#  Story Author: Conway Tan, Alan Li
#  Script Author: Wentian Bu
#  Version: 1.0
#  
# ###########################################################

label dj_105:

label .s68villa:
    show text "2018年10月5日" at truecenter with dissolve
    pause 0.5
    image time3 = Text("中午11:20", xalign=0.5, yalign=0.55)
    show time3 with dissolve
    pause 1.0

    scene ghost room sleep with Fade(1.0, 0.0, 0.8)
    "等睡醒已经是快十二点了，这次很幸运的是，你没有再陷入奇怪的梦境。"

    scene ghost livingroom light with dissolve
    "离开小别墅前，你打算再次前往晚上发现奇怪花纹的那个角落里查看。"
    "靠近时你有些忐忑不安，等到了那里，却长舒一口气，那个角落，并没有什么奇怪的花纹！"
    scene ghost door light with dissolve
    "但奇怪的是，等你回到储物间的门口，发现把手上的钥匙又一次不见了，门重新被上了锁。"
    me"大家有人把这上面的钥匙拿走了？"
    ming"拿走珍藏做纪念品吗？"
    lian"这上面还有钥匙？我记得房东锁着在吧。"
    "这也太诡异了……"
    wqbh"我刚才查了一下你说的那个人人网帖子，标题能搜出来，但内容已经404了。"
    me"不会吧？"
    "打开手机，从历史记录中访问，发现那个帖子真的已经不能访问了。"
    "你把花纹和钥匙消失的事情告诉了万千，你们决定不再告诉别人这个故事，就让它成为永久的迷，藏在心里吧。"
    stop music fadeout 2.0
    jump .s69street

label .s69street:
    scene zs typhoon1 with fade
    play music must_survive fadein 2.0
    "走出小别墅，你们才发觉台风真的要来了。"
    "外面下着大雨，狂风刮的大家东倒西歪，完全撑不住伞，甚至金蛋伞的龙骨都被风吹折了！"
    "这里似乎荒无人烟，在公交站等待了许久也没有一辆车，而且更糟糕的是，完全没有出租的踪影。"
    nuo"我都要被吹飞起来了，真羡慕壮壮的体重"
    zhuang"？？？放屁"
    han"原来台风这么可怕"
    yinyin"小明你不是之前有司机小哥的电话吗？"
    ming"对嗷，我差点都忘了，赶紧的安排上"
    lian"那我们不等公交算了，找个避风的地方等出租吧。"
    scene zs typhoon2 with dissolve
    "众人于是朝前逆风而行，终于找到了一个小区停自行车的雨棚。"
    ming"小哥说凑四辆出租有点难，让我们耐心点等着，有消息告诉我们。"
    "于是冠军拿出手机，开始进入主播模式："
    conway"大家好，我们现在在舟山，遭遇了平生第一次台风，你们看，这是台风下的树，这是台风下的自行车，这是台风下的我们..."
    jump .s70taxi

label .s70taxi:
    scene taxi typhoon with fade
    "不知过了多久，出租小哥车队终于来了，我们依次上了车，前往舟山客运站。"
    "小哥""你们有没有感觉这个车在飘？"
    me"……"
    "小哥""会开车的话一定要在台风天试一次，真的贼爽，有种开飞机的感觉"
    me"……"
    "小哥""你们要不要出租直接给送到宁波火车站啊，500块就行。"
    me"我们已经预定大巴票了...好像也没法子退"
    "小哥""我就随便一问，预定了就算了嘛。"

    "前面的小组好像已经到了，带来了一个坏消息："
    conway"车站已经贴出告示了，说台风天气，客运大巴全部停运。"
    conway"全车站都是滞留的旅客，连离开车站都要排很长的队打出租。"
    "小哥""是的，这种天气大巴车估计不敢上大桥，马上就要到车站了，你们还按原计划下吗？"
    ## May lead to different endings
    ## AutoSave 12-4
    menu:
        "时间紧急，要赶紧做决定："
        "还是到车站下吧，至少车站有麦当劳吃，大家汇合在一起也好商量":
            $ dunhuang_value -= 10
            "你们在车站汇合了，很开心又见到了大家，但..."
            jump dj_endings.end03

        "下车了估计就再也打不到出租了...我们还这么多人，肯定困在车站了":
            pass

    me"师傅，你看还有什么办法吗？"
    "小哥""大巴车肯定上不了桥，受风面太大了，但如果桥没封的话，我们出租应该还是可以冒下风险。"
    me"真的吗！那太好了！！"
    "小哥""都是来玩的，总不能让你们滞留在舟山吧。"
    me"我们一行可能需要四辆车，不知道您能不能再叫三个朋友把我们都送到宁波吗？"
    "小哥""我问问吧，这个天气人家不一定愿意……"
    me"确实可以理解，您看辛苦费要加多少"
    "小哥""这样，我之前说500，咱这辆车就按500算吧，我也不好意思多加钱。"
    "小哥""但其他人，我也说不清楚，我先按两百加，如果能找到，就700，找不到的话我们只能再商量了。"
    me"行，麻烦您了。"

    "你在群上告知了众人这个情况，大家都很高兴，真的太幸运了。"
    lian"那我们现在需要先确认舟山大桥有没有封。"
    $ menuset = set()
    ## Not important
    menu .s70taxi_ques:
        set menuset
        "这时你准备怎么办？"
        "打开微博搜搜有没有官号发布舟山大桥封锁的消息":
            nuo"不行啊，我在微博上没有刷到相关消息"
            jump .s70taxi_ques

        "拨打百度查到的管理处电话":
            han"一直占线打不通啊……"
            jump .s70taxi_ques

        "打110求救警察叔叔":
            $ dunhuang_value += 10
            zhuang"喂警察叔叔吗，请问舟山大桥现在小车能上吗？可以啊？谢谢！"
            wqbh"卧槽你这么熟练的吗？"
            zhuang"小意思，我经常打"
            
        "请司机小哥帮忙打听":
            $ dunhuang_value -= 10
            "小哥""我这也只能问问同事们，他们也都是没有准确消息。"
            jump .s70taxi_ques
    "终于确定了小车能上桥，司机小哥也陆续联系到了其他三辆车，正在朝客运站这边赶过来接大家。"
    jump .s71station

label .s71station:
    scene zs station with fade
    "在汽车站的众人喜出望外，闲聊间，旁边两位小姐姐问是否可以让她们也拼个车。"
    ## Not important
    menu:
        "这时你们准备？"
        "救人一命胜造七级浮屠，刚好还有两个空位，上吧":
            "她们很开心跟你们一起等车，后来发现都是同龄人，几位单身小哥哥都要了她们的联系方式……"
            
        "现在价格也还没有确定，也不方便跟她们分摊钱啊":
            $ dunhuang_value -= 20
            "你们婉拒了她们的请求，第二天你们看到新闻发现滞留舟山客运站的游客非常艰难，内心有些许愧疚。"

        "小姐姐？哪来的小姐姐？你给我找一个？":
            $ dunhuang_value += 10
            "仔细一看，才发现她们并没有跟你们说话……"
    "联系的出租终于到达客运站附近了，这时师傅说不能进站，要不然被其他人拦下他们不能拒载的，需要走一小段到旁边的酒店等候。"
    jump .s72station

label .s72station:
    scene zs typhoon building with fade
    """
    台风愈发严重了，外面风雨交加，人几乎睁不开眼。

    路上各种回旋气流裹挟着落叶、垃圾之类，行道树都几乎直不起来。

    沿着建筑的墙根，你们终于到了酒店门口。
    """

    "突然间从头顶上落下了许多白色的东西，就摔在你们刚才路过的地方！" with vpunch
    
    shou"我靠，这也太险了"
    """
    众人也唏嘘不已，定眼一看，原来是酒店高层的外围墙壁掉落了下来。

    不管怎么样，你们终于都依次上了出租，朝着舟山大桥进发。

    车一直在飘，几乎能明显感到水平方向的抖动，但总算是上了大桥，暂时还没有封桥真是万幸。

    终于，经过两小时的车程，你们到达了宁波市区，在吃过小杨生煎之后，车次在今天的连南等人，与众人分手离去。
    """
    stop music fadeout 2.0
    jump .s73nbstreet

label .s73nbstreet:
    scene nb street with fade
    play music melody_of_night_17 fadein 2.0
    "跟连南他们作别后，你们剩下的一行人还没想好今天晚上要去哪。"
    conway"和我在宁波的街头走一走污喔污喔，直到所有的灯都快要熄灭了也不停留..."
    han"打住，打住。我们今天晚上怎么搞？"
    "然而大家都拿不定主意，现在去找民宿显然来不及，而住酒店又不确定人家能不能允许大床房睡七八个人，更何况是男女混住……"
    conway"要不我们去KTV搞一夜吧？然后，可以再去捞！"
    ## May jump story
    ## AutoSave 12-5
    menu:
        "这时你说："
        "几天都没合眼了，我们还是找个地方睡吧...":
            $ dunhuang_value -= 10
            conway"桃神别嘛，去KTV多好，一起听歌一起睡……"
            shou"行了行了，桃神累了就让他去休息吧，我们去玩就行了。"
            "于是你和大家分开了，自己在附近找了一个酒店，住进去以后你很快进入了梦乡。"
            "早上醒来听说大家要去天一阁，你也打车去了。"
            jump .s76nbtower

        "好累啊，不过我还可以冲冲冲...":
            pass

        "我觉得ojbk！谁睡谁是狗，决战到天亮！":
            $ dunhuang_value += 20
            
    "大家纷纷露出敦煌本性支持这个决定。"
    conway"海底捞251号...排上了"
    shou"那得排多久啊……"
    han"没事，我们去KTV唱几个小时就行了，这家四小时欢唱才200块。"
    "众人""冲冲冲！"
    "……"
    jump .s74ktv

label .s74ktv:
    scene nb ktv with fade
    """
    你们来到KTV，这里的环境确实不错。

    你本来有些拘束，毕竟自己唱的不怎么样，但是看到冠军唱完后你变得信心满满！

    《平凡之路》、《成都》、《干杯》...唯一的遗憾是没有听到小萝卜的《死了都要爱》。

    诺诺把你唱歌的视频拍下来发到了“老百姓和小精灵”的群里，已经在候车的万千儿不停地回复：羡慕！想来玩！好听！
    """
    ## May lead to different endings
    ## AutoSave 12-6
    menu:
        "这时你："
        "第一次有人说自己唱歌好听，你在群里面回复她：终于遇到知音了！！":
            $ dunhuang_value += 10
            "万千儿回复你说：哦，不，我说的是冠军唱的好听。"

        "你很感谢万千儿对你的认可，于是你发了三个爱心":
            jump dj_endings.end04

        "你开玩笑地说，你快回来，一起唱。":
            """
            过了许久，万千私聊你说：我还在宁波站，还没走。

            你总觉得这是什么暗示，而且恰好戳中了你心里一块隐秘的地方。

            你试探性地回复：“等我”。

            她秒回说：“好”。

            于是你打了一辆出租车赶往宁波站。
            """
            scene nb station night with fade
            "在宁波站前，你见到了正准备进站的万千儿，你气喘吁吁。"
            wqbh"你还真来了？"
            me"是啊，我..."
            """
            话没说完，她便说道：“再见桃神！下次再见！我们还会见的~” 
            说罢，她边挥手边回头看你。

            你不知道她这段话究竟是什么意思，你想不明白，她究竟懂不懂你的意思呢？

            你想着这些，打了辆出租车回去了....
            """
            scene nb ktv with fade
            
    "两点钟了，大家都困的不行了。"
    conway"还有三桌了！准备出发了！"
    yinyin"冲冲冲！"
    ming"快走快走"
    "……"
    jump .s75hdl

label .s75hdl:
    scene nb hdl with fade
    "服务员把你们带到了座位上，照例还是牛油+番茄双锅底。"
    "这时，诺诺已经困的不行，蜷在角落里快要睡着的样子。"
    ## Not important
    menu:
        "这时你："
        "把她喊醒，不然少一个人A，血亏！ ":
            $ dunhuang_value += 10
            "你喊了半天她始终不醒，没办法，你只能让她躺在那里。"

        "让她躺着睡吧，不管她，反正小明还在呢":
            pass

        "喊服务员送五床被子":
            $ dunhuang_value += 20
            "服务员""不好意思我们这是宁波海底捞，不是宜城蓝波湾。"
            "服务员""但是我们有小毯子，我给您拿来吧。"

    ming"唉算了让她睡吧，我们吃吧。"
    "她盖着服务员送来的毯子睡着了，不一会儿发出打鼾的声音。"
    "菜很快就上来了，原本精疲力尽的大家，这会儿居然都来了精神："
    "一盘捞派滑牛肉本来需要煮五分钟，开始刚下去没到两分钟，小明就拿着筷子在锅里搅来搅去。"
    ## Not important
    menu:
        "大家见此状也赶快下去捞，这时你："
        "赶快捞！晚了就没有了！":
            $ dunhuang_value += 20
            "你看到大家都在捞，你也不甘示弱，赶快一起参与了抢食之中。"
            "半个小时后，你觉得肚子有点疼，于是去上厕所了。"

        "还没熟，我再等等":
            $ dunhuang_value -= 30
            "虽然大家都在捞，但是吃不熟的东西肯定要拉肚子的，小明真是狗屎！"
        
        "先把筷子伸下去搅着再说":
            $ dunhuang_value += 10
            "管他三七二十一，先搅着不夹起来，煮熟了才能吃。"

    "大家才不管你干什么，还泛着血色的肉片夹到碗里直接开吃。就这样，上来的一道道菜很快就光盘了。"
    "……"
    "不知不觉凌晨三点半了，大家都吃累了，挺着肚子倒在座位上。"
    "韩韩往厕所去了，并发出预警："
    han"刚才吃的肉可能没煮熟，我肚子有点疼。"
    yinyin"卧槽我好像也有点疼"
    shou"艹我也有点，都怪冠军！没熟就在那里捞捞捞！"
    conway"？？？你们肠胃不好怪我咯？我咋没事？"
    "大家先后奔向厕所去了，唯独小明和冠军没事。"
    "五点半了，临海的宁波东方既白。韩韩喊上淫淫准备去网吧打游戏。走了两个人，你终于能够躺下来睡一会儿了。"

    """
    过了不知道多久，你睡醒了，在海底捞睡觉就跟回家一样，就是安心，之前噩梦缠身的你很是满意。

    你突然发现阿郑和韩韩已经离开了……一个个都走了，连个招呼都没打，下次再见面又得是什么时候…
    
    你不禁有一丝想哭。

    你在脑海中回顾了一下这几天的经历，真的充满了艰辛和刺激，每天的平均睡眠时间不超过5个小时。这哪里是旅游放松，简直是极限挑战。
    
    此刻的你又累又困，很想找个像南京不纸书店或者北京汤泉良子的地方好好休息一下……
    """
    conway"我们大家好像都是下午两三点的车吧？我们上午再去玩会儿吧。"
    nuo"啊？还玩啊，我好困"
    ming"你都睡了一晚上了"
    nuo"睡得不舒服啊..."
    conway"来都来了……"
    shou"那我们去天一阁吧！宁波最有名的地方，在建筑学里面……"
    "大家都默默听着兽兽的专业解说，那就去吧。"
    "外面的天还是蒙蒙亮的状态，10月的宁波在这个时候还有些冷，你们叫了两辆车一起前往天一阁。"
    jump .s76nbtower

label .s76nbtower:
    scene nb tower with fade
    """
    你们打车来到了天一阁，可是因为太早了，居然没有开门！

    而这里也没有其他等待的游客，于是你们便坐在台阶上等着。

    距离九点钟开门还有半个小时，此刻的大家又累又困，于是便在地上坐下，东倒西歪地靠在一起。

    你见冠军抱着头埋在腿上，小明和诺诺互相靠在一起，兽兽和小老弟靠在柱子上，一人一边，俨然一副“事后”的感觉。

    开门的时间越来越近，人渐渐多了起来。保安大哥注意到你们并向你们走过来……
    """
    "保安""你们是一起的？他们怎么了这是？"
    ## Not important
    menu:
        "你回答："
        "他们久仰天一阁大名，离开宁波前一定要来拜访":
            $ dunhuang_value += 10
            "保安大叔听到后十分感动，拿起对讲机向经理汇报了情况。"
            "经理当即决定提前开门，并配备VVIP导游，带领你们游览。"

        "是是是，我们是一起的，您别担心":
            $ dunhuang_value -= 10
            "保安大叔摇摇头："
            "保安""现在的年轻人啊，身体又不行，还喜欢乱搞...真的是..."
            "你听完哭笑不得，后悔没把这话录下来。"

        "我不认识他们...":
            "保安大哥愈加紧张了，这几个人在这里不会是想搞黄色吧，呸搞坏事吧。"
            "他拿起对讲机准备喊来更多兄弟，你见状赶紧阻止了他，并解释了原因。"
            "保安大哥听后觉得莫名其妙，摇摇头走开了。"

    """
    你叫醒大家准备进去参观。此刻的大家虽然睁着眼睛，但是眼神中透着迷离，四肢酸乏无力。

    好在兽兽对天一阁很感兴趣，这时是清醒的，于是你和兽兽带着剩下几个人走进景区里面，像是湘西赶尸的。

    渐渐多起来的游客看着你们都觉得莫名其妙，所以只敢远远地躲着，你和兽兽不好意思地向其他游客微笑。
    """
    jump .s77nbtower

label .s77nbtower:
    scene nb tower mountain with fade
    "冠军突然猛地一下清醒了，开始蹦蹦跳跳地："
    conway"前面有个假山，我们爬上去吧！"
    "其他几个人还处在梦游状态，神志不清的。你本想推托，但只见冠军已经开始爬山了，其他人也不受控制地跟着冠军走。"
    "走到半路，刚才还元气十足的冠军突然一下子蔫了，说话也含糊不清地："
    conway"走啊...爬啊...来都来了...."
    "这并不是一个很高的假山，站在山顶，可以看到楼榭对面的风景。"
    "你拍了几张好看的照片，正准备下去的，却发现他们已经趴在旁边的桌子上睡着了。小老弟则趴在石头上睡着了。"
    "你和兽兽喊了好几声，他们都不应你。"
    ## Not important
    menu:
        "这时你："
        "算了，让他们在这趴会吧，你和兽兽去逛一逛。":
            pass

        "天啊，他们不会猝死吧！":
            $ dunhuang_value -= 10
            "你和兽兽四目相对，你感觉上去摸摸他们的气息，靠近时居然听到诺诺在打鼾……"
            nuo"呼……呼……呼……"

        "大叫一声把他们吓醒！":
            $ dunhuang_value += 10
            "结果大家纹丝不动，反而把正准备上来的游客吓到了，他们嫌弃地看着你……"

    shou"算了，别管他们了，让他们睡会儿，我们去逛逛吧。"
    "于是你和兽兽把他们抛弃在这里，你们俩人去天一阁逛了。"

    scene nb tower with fade
    "天一阁院子并不大，你们虽然走的很慢但也不知不觉到了出口处，这才想起来还有几个“丧尸”留在入口那里着。"
    ## Not important
    menu:
        "这时你："
        "这么久了也不联系我们，不会出什么事了吧？赶快回去看看":
            $ dunhuang_value -= 10
            "你和兽兽觉得形势不妙，赶紧往回走。这时你听到门外传来淫淫的声音..."

        "算了，自己也走累了，到出口等他们吧":
            $ dunhuang_value += 10
            "你和兽兽边打电话，边往出口走去..."

    "他们居然已经在出口外面了！你和兽兽觉得莫名其妙。"
    shou"卧槽，你们咋比我们还快。"
    ming"男人不说快。"
    yinyin"我们在那趴了一会，过会就看不到你们了，然后我们走走马观花走到出口了..."
    conway"诶呀我觉得没逛好，我们要不再回去走一趟？"
    shou"诶诶诶打住打住！"
    me"算了，我们找个地方吃饭吧，早点去火车站，免得又误车了，毕竟今天诺诺也在。"
    nuo"感觉有被冒犯。"
    "你们叫了两辆车，找了下最近的商圈，去吃了点宁波菜当午饭。"
    stop music fadeout 2.0
    jump .s78taxi

label .s78taxi:
    scene taxi day with fade
    play music hhwq fadein 2.0
    """
    去往火车站意味着离别的时刻要来了。

    这趟旅程很短暂，又充满了惊喜、惊吓、奇遇、冒险，还有一些神秘的东西...但和这群人在一起，一切都顺利解决，螺旋稳。

    从天南海北聚集到这里的人又将要回到各自的地方去，可能以后很难再有机会和这样一群人做这样冒险的事情了……
    
    你戴上耳机，应景地响起了邓紫棋的《后会无期》。

    车上有些堵，但好在留的时间很充裕。
    
    都要走了，总不会再发生什么事情吧……
    """

    "司机""诶呀，这条路有点堵，我估计还得半个小时。"
    ming"啊？这么久，我们火车还有五十分钟就开了..."
    "司机""那稍微有点悬。"
    me"啊？你们不是下午三点的吗？"
    ming"小老弟是下午三点的，我和诺诺去南京，两点钟的。"
    me"你们还要去南京玩啊？！太浪了吧。"
    "看来和诺诺一起走会误车的魔咒还是没被打破...都要走了居然还能搞出点幺蛾子。"
    "……"
    "师傅又开了二十五分钟，眼看着开车时间越来越近了。"
    conway"师傅，还有多远？"
    "司机""800米吧大概。"
    ming"那要不你让我们下来，我们跑过去算了，这里这么堵。"

    nuo"我觉得可以，我们跑吧！"
    "你觉得诺诺说的“我可以”一般没啥用，{w}就像你们做《大樱桃纪念册-东极篇》的时候，她说了无数次“我可以写剧本”，但最终也就写了200多个字，不靠谱。"
    "司机""你们要是赶的话就跑过去吧，从那边走，300米。"
    """
    你们赶快背上包，开门跳下车，沿着马路的右侧飞奔着向火车站跑出。

    后面一辆车上的淫淫、小老弟和兽兽也跳下了车。七个人，在高架上上演着速度与激情。
    """
    jump .s79station

label .s79station:
    scene nb station day with fade
    "气喘吁吁地跑道宁波站，冠军和小明赶紧去找取票机，正准备跑过去。诺诺突然叫住了他们。"
    nuo"你们看！"
    nuo"宁波到南京，14:32开，晚点40分钟。"
    ming"草草草。"
    yinyin"牛逼啊！"
    nuo"你看，跟着我会有好运的！"
    conway"螺旋稳啊！"
    "你们庆幸着这趟旅程终结之时居然还有幸运发生，大家取完票朝候车厅走去。"

    scene nb station photo with fade
    "大家坐在座位上，安安静静地，诺诺趴在行李箱上睡着了。"
    conway"我们来合个影吧！记录一下这趟不凡之行。"
    "3、2、1！"
    play sound take_photo
    scene nb station photo with phototake
    "照片定格下了这个时刻。"

    scene nb station plat with Fade(2.0, 0.5, 1.5)
    "去往南京的火车和去往北京、天津的火车都开始检票了。你的车也还有十分钟就要发车了。大家相互挥手作别。"
    conway"下次再见！"
    ming"下次见，拜拜！"
    me"下次见！"
    "可是不知下次再见又是何时了……"
    "你赶快朝自己车次的检票口奔去，检票上了车。"
    jump dj_endings.end05

